plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.8.8'
    id 'idea'
}

group 'home'
version '0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

def grpcVersion = '1.28.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.11.0'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.28.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task grpcServer(type: CreateStartScripts) {
    mainClassName = 'home.GrpcServer'
    applicationName = 'grpc-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task grpcClient(type: CreateStartScripts) {
    mainClassName = 'home.GrpcClient'
    applicationName = 'grpc-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task grpcChatClient(type: CreateStartScripts) {
    mainClassName = 'home.ChatClient'
    applicationName = 'grpc-chat-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task grpcChatServer(type: CreateStartScripts) {
    mainClassName = 'home.ChatServer'
    applicationName = 'grpc-chat-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(grpcServer)
    from(grpcClient)
    from(grpcChatServer)
    from(grpcChatClient)
    fileMode = 0755
}



